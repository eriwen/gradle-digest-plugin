plugins {
    id "java-gradle-plugin"
    id "groovy"
    id "idea"
}

group = "com.eriwen"
version = "0.0.1"

apply from: "$rootDir/gradle/integration-test.gradle"
apply from: "$rootDir/gradle/functional-test.gradle"
apply from: "$rootDir/gradle/additional-artifacts.gradle"
apply from: "$rootDir/gradle/publishing.gradle"
apply from: "$rootDir/gradle/release.gradle"

repositories {
    jcenter()
}

dependencies {
    compile "commons-codec:commons-codec:1.10"
    testCompile localGroovy()
    testCompile("org.spockframework:spock-core:1.0-groovy-2.4") {
        exclude module: "groovy-all"
    }
}

ext.compatibilityVersion = "1.8"
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

jar {
    manifest {
        attributes "Implementation-Title": "Gradle Digest plugin",
                "Implementation-Version": version,
                "Built-By": System.getProperty("user.name"),
                "Built-Date": new Date(),
                "Built-JDK": System.getProperty("java.version"),
                "Built-Gradle": gradle.gradleVersion
    }
}

gradlePlugin {
    plugins {
        digest {
            id = "com.eriwen.gradle.digest"
            implementationClass = "com.eriwen.gradle.DigestPlugin"
        }
    }
}

idea.project {
    jdkName = "1.8"
    languageLevel = compatibilityVersion
    vcs = "Git"

    ipr.withXml { provider ->
        def node = provider.asNode()

        // Set Gradle home
        def gradleSettings = node.appendNode("component", [name: "GradleSettings"])
        gradleSettings.appendNode("option", [name: "SDK_HOME", value: gradle.gradleHomeDir.absolutePath])

        // set compiler heap space
        def javacSettings = node.appendNode("component", [name: "JavacSettings"])
        javacSettings.appendNode("option", [name: "MAXIMUM_HEAP_SIZE", value: "256"])
    }
}

task install(dependsOn: 'publishToMavenLocal')

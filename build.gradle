plugins {
    id "java-gradle-plugin"
    id "groovy"
    id "com.gradle.plugin-publish" version "0.9.5"
}

group = "com.eriwen"
version = "0.0.2"

ext.compatibilityVersion = "1.7"
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

apply from: "$rootDir/gradle/additional-artifacts.gradle"
apply from: "$rootDir/gradle/idea.gradle"
apply from: "$rootDir/gradle/publishing.gradle"
apply from: "$rootDir/gradle/release.gradle"

repositories {
    jcenter()
}

dependencies {
    compile "commons-codec:commons-codec:1.10"
    testCompile localGroovy()
    testCompile("org.spockframework:spock-core:1.0-groovy-2.4") {
        exclude module: "groovy-all"
    }
}

jar {
    manifest {
        attributes "Implementation-Title": "Gradle Digest plugin",
                "Implementation-Version": version,
                "Built-By": System.getProperty("user.name"),
                "Built-Date": new Date(),
                "Built-JDK": System.getProperty("java.version"),
                "Built-Gradle": gradle.gradleVersion
    }
}

gradlePlugin {
    plugins {
        digest {
            id = "com.eriwen.gradle.digest"
            implementationClass = "com.eriwen.gradle.DigestPlugin"
        }
    }
}

pluginBundle {
    website = "https://github.com/eriwen/gradle-digest-plugin"
    vcsUrl = "https://github.com/eriwen/gradle-digest-plugin"
    description = "Digest sources to allow identification of files by their content"
    tags = ["asset-versioning", "caching", "md5", "sha256", "sha512"]

    plugins {
        digest {
            id = "com.eriwen.gradle.digest"
            displayName = "Gradle Digest Plugin"
        }
    }
}
